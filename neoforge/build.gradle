plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

File generatedResources = file("../common/src/generated")
File defaultResources = file("../common/src/main/resources")
loom {
    runs {
        register("data") {
            data()
            programArgs("--all", "--mod", "mega_showdown")
            programArgs("--output", generatedResources.absolutePath, "--existing", defaultResources.absolutePath)
        }
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$project.neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$project.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    forgeRuntimeLibrary("thedarkcolour:kotlinforforge-neoforge:${project.kotlin_neo_version}") {
        exclude group: "net.neoforged.fancymodloader", module: "loader"
    }

    modImplementation "io.wispforest:accessories-neoforge:${project.accessories_version}"
    forgeRuntimeLibrary("blue.endless:jankson:1.2.2")
    forgeRuntimeLibrary("io.wispforest:endec:0.1.5.1")
    forgeRuntimeLibrary("io.wispforest.endec:gson:0.1.3.1")
    forgeRuntimeLibrary("io.wispforest.endec:jankson:0.1.3.1")
    forgeRuntimeLibrary("io.wispforest.endec:netty:0.1.2")

    modImplementation "com.cobblemon:neoforge:${project.cobblemon_version}"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

sourceSets {
    main {
        resources {
            srcDir "../common/src/main/resources"
        }
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}